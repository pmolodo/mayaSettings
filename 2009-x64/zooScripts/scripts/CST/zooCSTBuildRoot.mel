zooArrays_str;
zooAttrUtils;
zooBrandTools;
zooCSTUtils;
zooFlags;
zooObjMenuUtils;
zooCSTPrimitives;
zooSpaceSwitching;
zooRenamerUtils;


//script documentation is at the end of this file
global proc string[] zooCSTBuildRoot( string $prefix, string $root, string $optionStr ) {
	//------
	//parse optionStr to determine which flags are enabled for this proc
	//------
		string $optionTemp[] = {};
		string $hips = "";
		string $colour = "darkblue 0.65";
		float $scale = 1;
		int $buildhips = 0;
		int $pickwalking = 1;
		int $isCurve = 0;

		$optionTemp = `zooGetFlagArguments $optionStr hips`; if( `size $optionTemp` ) if( `objExists $optionTemp[0]` ) $hips = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr colour`; if( `size $optionTemp` ) $colour = `zooArrayToStr_str $optionTemp " "`;
		$optionTemp = `zooGetFlagArguments $optionStr buildhips`; if( `size $optionTemp` ) $buildhips = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr pickwalking`; if( `size $optionTemp` ) $pickwalking = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr scale`; if( `size $optionTemp` ) $scale = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr curve`; if( `size $optionTemp` ) $isCurve = $optionTemp[0];
		if( !`objExists $hips` || $hips == $root ) $buildhips = 0;


	//------
	//create the controls, and parent them
	//------
		string $rootControl = `zooBuildControl ( $prefix +"_root_ctrl" ) ( "-type ring -place "+ $root +" -scale "+ (3.1*$scale) +" -colour "+ $colour )`;
		string $rootGimbal = `zooBuildControl ( $prefix +"_root_gimbal_ctrl" ) ( "-type starCircle -place "+ $root +" -scale "+ (4*$scale) +" -surface 0 -colour "+ $colour )`;
		string $rootSpace = `zooBuildControl ( $prefix +"_root_space_ctrl" ) ( "-type null -place "+ $root )`;
		string $hipsControl = "";

		parent $rootGimbal $rootControl;
		parent $rootControl $rootSpace;

		if( $buildhips ) {
			$hipsControl = `zooBuildControl ( $prefix +"_hips_ctrl" ) ( "-type cube -place "+ $root +" -placepivot %p -scale "+ $scale +" -offset -0.75up -colour "+ $colour )`;
			parent $hipsControl $rootGimbal;
			zooAttrState "-attrs t s -k 0 -l 1" $hipsControl;
			zooAttrState "-attrs v -k 0" $hipsControl;
			}


	//------
	//turn unwanted transforms off, so that they are locked, and no longer keyable
	//------
		string $turnTranslateOff[] = { $rootGimbal };
		string $turnScaleOff[] = { $rootControl, $rootGimbal };

		for ( $obj in $turnTranslateOff ) zooAttrState "-attrs t -k 0 -l 1" $obj;
		for ( $obj in $turnScaleOff ) zooAttrState "-attrs s -k 0 -l 1" $obj;
		for ( $obj in $turnScaleOff ) zooAttrState "-attrs v -k 0" $obj;

		string $rootGimbalShape = zooGetElement_str( 0,`listRelatives -s $rootGimbal` );
		setAttr ( $rootGimbalShape +".v" ) 0;
		xform -p 1 -roo xzy $rootControl;
		xform -p 1 -roo zxy $rootGimbal;


	//------
	//build pickwalking if required
	//------
		if( $pickwalking ) {
			zooPickwalk "-load";
			zooSetPickwalkObj $rootGimbal $rootControl "-dir up -reciprocal 0";
			}


	//------
	//add right click menu to turn on the gimbal control
	//------
		string $cmdName = "toggle gimbal control";
		string $cmd1Str =  "{\nstring $kids[] = `listRelatives -type transform #`;\n$kids = `listRelatives -s $kids[0]`;\nint $vis = `getAttr ( $kids[0] +\".v\" )`;\nfor( $k in $kids ) setAttr ( $k +\".v\" ) (!$vis);\n}";
		string $cmd2Str =  "{\nstring $kids[] = `listRelatives -s #`;\nint $vis = `getAttr ( $kids[0] +\".v\" )`;\nfor( $k in $kids ) setAttr ( $k +\".v\" ) (!$vis);\nselect `listRelatives -p`;\n}";
		int $cmd1Num = `zooObjMenuAddCmd $rootControl`;
		int $cmd2Num = `zooObjMenuAddCmd $rootGimbal`;

		zooSetObjMenuCmdName $rootControl $cmdName $cmd1Num;
		zooSetObjMenuCmdStr $rootControl $cmd1Str $cmd1Num;
		zooSetObjMenuCmdName $rootGimbal $cmdName $cmd2Num;
		zooSetObjMenuCmdStr $rootGimbal $cmd2Str $cmd2Num;


	//------
	//now brand all objects
	//------
		string $controllers[] = { $rootControl, "", $rootSpace, $rootGimbal };
		if( $buildhips ) $controllers[1] = $hipsControl;
		for( $obj in $controllers ) zooBrandObject name $prefix $obj;
		zooBrandObject root $prefix $rootControl;
		zooBrandObject rootGimbal $prefix $rootGimbal;
		if( $buildhips ) zooBrandObject hips $prefix $hipsControl;


	//------
	//cleanup - put all control objects where they should be in the outliner
	//------
		for( $n=0; $n<`size $controllers`; $n++ ) $controllers[$n] = `zooRenamerFunctions replace "[0-9]+$" "" $controllers[$n]`;

	return $controllers;
	}


//zoo.end
//macaroniKazoo ©2007
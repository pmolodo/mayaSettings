zooArrays_str;
zooVectors;
zooFlags;


//------
//this general purpose proc aligns two objects - it uses parent constraint (on an intermediate object) so its rotation order independent
//------
global proc zooAlign( string $optionStr ) {
	//------
	//parse optionStr to determine which flags are enabled for this proc
	//------
	string $selObjs[] = `ls -sl`;
	string $temp[] = {};
	string $src = $selObjs[0];
	string $tgts[] = `zooIndexCrop_str $selObjs "1:"`;
	int $pivot = 0;
	int $key = 0;

	$temp = `zooGetFlagArguments $optionStr load`; if( `size $temp` ) if( $temp[0] == "1" ) return;
	$temp = `zooGetFlagArguments $optionStr src`; if( `objExists $temp[0]` ) $src = $temp[0];
	$temp = `zooGetFlagArguments $optionStr tgt`; if( `size $temp` ) $tgts = $temp;
	$temp = `zooGetFlagArguments $optionStr pivot`; if( `size $temp` ) $pivot = $temp[0];
	$temp = `zooGetFlagArguments $optionStr key`; if( `size $temp` ) $key = $temp[0];
	for( $tgt in $tgts ) zooAlignFast $src $tgt $key $pivot;
	}


//------
//this is separated into a separate proc, because its less "user friendly" ie require less syntax to work
//its better to call zooAlign in non speed intensive operations because the syntax for this command may change
//if the scope of the script is expanded in future - ie I add more functionality
//------
global proc zooAlignFast( string $src, string $tgt, int $key, int $pivotOnly ){
	//these two lines check to make sure the objects to be aligned are both transform (or joint) nodes, otherwise, it quits
	if( !`objExists $src` || !`objExists $tgt` ) return;
	if( !`objExists ( $src +".t" )` || !`objExists ( $tgt +".t" )` ) return;

	//get a list of the selected object, and duplicate the target object - we duplicate the target so we're
	//free to do whatever we want to it, without disturbing any animation or connections on the actual target
	string $selObjs[] = `ls -sl`;
	string $dupe = zooGetElement_str(0,`duplicate -rc $tgt`);

	//create a list of all the axes to look at - we will check all these axes to make sure they're not locked
	//creating a constraint on a locked axis will give us an error
	string $axes[] = { "tx","ty","tz","rx","ry","rz" };
	int $lok[] = {};

	//go through the list of axes above, and build an array that tells us which axes aren't settable
	for( $a in $axes ) $lok[( `size $lok` )] = `getAttr -se ($tgt +"."+ $a)`;

	//parent constrain the target object to the source object, making sure to skip over the axes that are locked
	if( $pivotOnly ) {
		float $pos[] = `xform -q -ws -rp $src`;
		xform -a -ws -piv $pos[0] $pos[1] $pos[2] $tgt;
		}
	else eval( "parentConstraint "+ ($lok[0]?"":"-st x ") + ($lok[1]?"":"-st y ") + ($lok[2]?"":"-st z ") + ($lok[3]?"":"-sr x ") + ($lok[4]?"":"-sr y ") + ($lok[5]?"":"-sr z ") + $src +" "+ $dupe );

	//now go over each axis, and if its not locked set the REAL target object to the transform values of the duplicate
	//target object.  this will make the real target aligned to the source object, all without having to add an
	//actual constraint to the target object itself
	//Note that we need to read all of the values before setting any of the REAL target values - otherwise, it's
	//possible that changing one of the attributes on the target will result in a change in one of the attributes
	//of our dupe before it is read - ie, if we are trying to align an object to the position of one of it's
	//children
	float $attrValues[];
	for( $n=0; $n<`size $axes`; $n++ ) if( $lok[$n] ) {
		$attrValues[$n] = `getAttr ($dupe +"."+ $axes[$n])`;
		}
	for( $n=0; $n<`size $axes`; $n++ ) if( $lok[$n] ) {
		setAttr ($tgt +"."+ $axes[$n]) $attrValues[$n];
		if( $key ) setKeyframe -at $axes[$n] $tgt;
		}

	//now we delete the duplicate target object, and select the objects that were selected before the script was run
	delete $dupe;
	select $selObjs;
	}


//does a simple align
global proc zooAlignSimple( string $src, string $tgt ){
	//these two lines check to make sure the objects to be aligned are both transform (or joint) nodes, otherwise, it quits
	if( !`objExists $src` || !`objExists $tgt` ) return;
	if( !`objExists ( $src +".t" )` || !`objExists ( $tgt +".t" )` ) return;

	float $pos[] = `xform -q -ws -rp $src`;
	float $rot[] = `xform -q -ws -ro $src`;

	move -rpr $pos[0] $pos[1] $pos[2] $tgt;
	rotate -a -ws $rot[0] $rot[1] $rot[2] $tgt;
	}


//------
//this proc will align the bones controlled by an ik chain to the fk chain
//flags used:
//-ikHandle  this flag specifies the name of the ikHandle to work on
//-onCmd     this flag tells the script what command to run to turn the ik handle on - it is often left blank because its assumed we're already in ik mode
//-offCmd    this flag holds the command to turn the ik handle off, and switch to fk mode
//NOTE: if the offCmd isn't specified, it defaults to:  if( `getAttr -se #.ikb` ) setAttr #.ikb 0;
//
//symbols to use in cmd strings:
// ^  refers to the ikHandle
//
//example:
//zooAlignFK "-ikHandle ikHandle1 -onCmd setAttr ^.ikBlend 1 -offCmd setAttr ^.ikBlend 0";
//------
global proc zooAlignFK( string $optionStr ) {
	string $temp[] = {};
	string $control;
	string $ikHandle;
	string $onCmd;
	string $offCmd = "if( `getAttr -se ^.ikb` ) setAttr ^.ikb 0;";
	int $alignEnd = 1;

	$temp = `zooGetFlagArguments $optionStr control`; if( `size $temp` ) $control = $temp[0];
	$temp = `zooGetFlagArguments $optionStr ikHandle`; if( `size $temp` ) $ikHandle = $temp[0];
	$temp = `zooGetFlagArguments $optionStr alignEnd`; if( `size $temp` ) $alignEnd = $temp[0];
	$temp = `zooGetFlagArguments $optionStr onCmd`; if( `size $temp` ) $onCmd = `zooArrayToStr_str $temp " "`;
	$temp = `zooGetFlagArguments $optionStr offCmd`; if( `size $temp` ) $offCmd = `zooArrayToStr_str $temp " "`;

	while( `match "#+" $onCmd` != "" ) $onCmd = `substitute "#+" $onCmd $control`;
	while( `match "#+" $offCmd` != "" ) $offCmd = `substitute "#+" $offCmd $control`;

	//the ^ symbol in either the onCmd or the offCmd is used to specify the ikHandle - so we must sub it into the command strings
	while( `match "\\^+" $onCmd` != "" ) $onCmd = `substitute "\\^+" $onCmd $ikHandle`;
	while( `match "\\^+" $offCmd` != "" ) $offCmd = `substitute "\\^+" $offCmd $ikHandle`;

	if( !`ikHandle -ex $ikHandle` ) return;
	if( `size $onCmd` ) eval $onCmd;
	string $joints[] = `ikHandle -q -jl $ikHandle`;
	string $effector = `ikHandle -q -ee $ikHandle`;
	string $effectorCtrl[] = `listConnections -d 0 ( $effector +".tx" )`;

	for( $n=0; $n<`size $joints`; $n++ ) {
		float $rotation[] = `getAttr ( $joints[$n] +".r" )`;
		if( `getAttr -se ( $joints[$n] +".rx" )` ) setKeyframe -at rx -v $rotation[0] $joints[$n];
		if( `getAttr -se ( $joints[$n] +".ry" )` ) setKeyframe -at ry -v $rotation[1] $joints[$n];
		if( `getAttr -se ( $joints[$n] +".rz" )` ) setKeyframe -at rz -v $rotation[2] $joints[$n];
		}

	if( $alignEnd ) zooAlign ( "-key 1 -src "+ $ikHandle +" -tgt "+ $effectorCtrl[0] );
	if( `size $offCmd` ) eval $offCmd;
	}


//------
//this proc will align the IK controller to its fk chain
//flags used:
//-control   this is the actual control being used to move the ikHandle - it is assumed to be the same object as the ikHandle, but if its different (ie if the ikHandle is constrained to a controller) use this flag
//-pole      tells the script the name of the pole controller - if there is no pole vector control, leave this flag out
//-ikHandle  this flag specifies the name of the ikHandle to work on
//-onCmd     this flag tells the script what command to run to turn the ik handle on - it is often left blank because its assumed we're already in ik mode
//-offCmd    this flag holds the command to turn the ik handle off, and switch to fk mode
//NOTE: if the offCmd isn't specified, it defaults to:  if( `getAttr -se ^.ikb` ) setAttr ^.ikb 1;
//
//symbols to use in cmd strings:
// ^  refers to the ikHandle
// #  refers to the control object
//
//example:
//zooAlignIK "-control somObj -ikHandle ikHandle1 -offCmd setAttr #.fkMode 0";
//------
global proc zooAlignIK( string $optionStr ) {
	string $temp[] = {};
	string $control;
	string $pole;
	string $ikHandle;
	string $onCmd;
	string $offCmd = "if( `getAttr -se ^.ikb` ) setAttr ^.ikb 1;";
	int $alignEnd = 1;

	$temp = `zooGetFlagArguments $optionStr control`; if( `size $temp` ) $control = $temp[0];
	$temp = `zooGetFlagArguments $optionStr pole`; if( `size $temp` ) $pole = $temp[0];
	$temp = `zooGetFlagArguments $optionStr ikHandle`; if( `size $temp` ) $ikHandle = $temp[0];
	$temp = `zooGetFlagArguments $optionStr alignEnd`; if( `size $temp` ) $alignEnd = $temp[0];
	$temp = `zooGetFlagArguments $optionStr onCmd`; if( `size $temp` ) $onCmd = `zooArrayToStr_str $temp " "`;
	$temp = `zooGetFlagArguments $optionStr offCmd`; if( `size $temp` ) $offCmd = `zooArrayToStr_str $temp " "`;
	if( !`objExists $control` ) $control = $ikHandle;

	//the ^ symbol in either the onCmd or the offCmd is used to specify the ikHandle - so we must sub it into the command strings
	//likewise for the # symbol which represents the control object
	while( `match "#+" $onCmd` != "" ) $onCmd = `substitute "#+" $onCmd $control`;
	while( `match "#+" $offCmd` != "" ) $offCmd = `substitute "#+" $offCmd $control`;
	while( `match "\\^+" $onCmd` != "" ) $onCmd = `substitute "\\^+" $onCmd $ikHandle`;
	while( `match "\\^+" $offCmd` != "" ) $offCmd = `substitute "\\^+" $offCmd $ikHandle`;

	if( !`ikHandle -ex $ikHandle` ) return;
	if( `size $onCmd` ) eval $onCmd;
	string $joints[] = `ikHandle -q -jl $ikHandle`;
	string $effector = `ikHandle -q -ee $ikHandle`;
	string $effectorCtrl[] = `listConnections -d 0 ( $effector +".tx" )`;

	if( `objExists $pole` ) {
		float $pos[] = `zooFindPolePosition ( "-start "+ $joints[0] +" -mid "+ $joints[1] +" -end "+ $effector +" -dist 1" )`;
		move -ws -rpr $pos[0] $pos[1] $pos[2] $pole;
		}

	zooAlign ( "-src "+ $effectorCtrl[0] +" -tgt "+ $control );
	if( `size $offCmd` ) eval $offCmd;
	}


//zoo.end
//macaroniKazoo ©2007
